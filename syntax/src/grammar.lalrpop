use basis::pos::{Spanned, Location};

use token::Token;
use ast::{Node, NodeKind, Type, Let, Def, Operator, Toplevel, ToplevelKind};

use MutNodeEnv;

grammar<'input, 'env>(src: &'input str, env: MutNodeEnv<'env>);

extern {
    type Location = Location;

    enum Token<'input> {
        "upper identifier" => Token::UpperIdentifier(<&'input str>),
        "Ref" => Token::RefType,
        "identifier" => Token::Identifier(<&'input str>),
        "int literal" => Token::IntLiteral(<i32>),
        "float literal" => Token::FloatLiteral(<f64>),
        "true" => Token::True,
        "false" => Token::False,

        "+" => Token::Add,
        "-" => Token::Sub,
        "*" => Token::Mul,
        "/" => Token::Div,
        "=" => Token::Equals,

        "==" => Token::EqEq,
        "!=" => Token::Neq,
        "<=" => Token::LE,
        "<" => Token::LT,
        ">=" => Token::GE,
        ">" => Token::GT,

        "if" => Token::If,
        "else" => Token::Else,
        "while" => Token::While,
        "let" => Token::Let,
        "def" => Token::Def,
        "print" => Token::Print,
        "ref" => Token::Ref,
        "@" => Token::Deref,
        "<-" => Token::LeftArrow,

        ":" => Token::Colon,
        "," => Token::Comma,
        "(" => Token::LParen,
        ")" => Token::RParen,
        "[" => Token::LBrack,
        "]" => Token::RBrack,
        "{" => Token::LBrace,
        "}" => Token::RBrace,

        ";" => Token::Semi,
        "newline" => Token::ImplicitSemi,
    }
}

Span<T>: Spanned<T> = {
    <l: @L> <t: T> <r: @R> => { Spanned::new(l, r, t) }
};

Sep = {
    ";",
    "newline",
};

SepSeq<R>: Vec<R> =
    <rs: (<R> Sep)*> <last: R?> => {
        let mut rs = rs;
        rs.extend(last);
        rs
    };

Comma<R>: Vec<R> =
    <rs: (<R> ",")*> <last: R?> => {
        let mut rs = rs;
        rs.extend(last);
        rs
    };

TypeSpec: Type = {
    ":" <typ:Type> => {
        typ
    }
};

Param: (String, Type) =
    <name:"identifier"> <typ:TypeSpec> => {
        (name.to_string(), typ)
    };

// types

PrimaryType: Type = {
    "upper identifier" => Type::new(<>.to_string()),
    "(" ")" => Type::new("()".to_string()),
};

RefType: Type = {
    "Ref" "[" <inner:Type> "]" => Type::newref(inner),
};

Type: Type = {
    PrimaryType,
    RefType,
};

// expressions

IfExpr: Node = {
    "if" <cond:Expression> <then:SpannedBlockExpr> => {
        Node::new(env.next_id(), NodeKind::If(Box::new(cond), Box::new(then), None))
    },
    "if" <cond:Expression> <then:SpannedBlockExpr> "else" <els:SpannedBlockExpr> => {
        Node::new(env.next_id(), NodeKind::If(Box::new(cond), Box::new(then), Some(Box::new(els))))
    },
    "if" <cond:Expression> <then:SpannedBlockExpr> "else" <elsif:SpannedIfExpr> => {
        Node::new(env.next_id(), NodeKind::If(Box::new(cond), Box::new(then), Some(Box::new(elsif))))
    },
};

SpannedIfExpr: Spanned<Node> = Span<IfExpr>;

WhileExpr: Node =
    "while" <cond:Expression> <body:SpannedBlockExpr> => {
        Node::new(env.next_id(), NodeKind::While(Box::new(cond), Box::new(body)))
    };

BlockExpr: Node = {
    "{" <nodes:Nodes> "}" => Node::new(env.next_id(), NodeKind::Block(nodes)),
};

SpannedBlockExpr: Spanned<Node> = Span<BlockExpr>;

Literal: Node = {
    <ident:"identifier"> => Node::new(env.next_id(), NodeKind::Ident(ident.to_string())),
    <v:"int literal"> => Node::new(env.next_id(), NodeKind::Int(v)),
    <v:"float literal"> => Node::new(env.next_id(), NodeKind::Float(v)),
    <v:"true"> => Node::new(env.next_id(), NodeKind::Bool(true)),
    <v:"false"> => Node::new(env.next_id(), NodeKind::Bool(false)),
    "(" ")" => Node::new(env.next_id(), NodeKind::Unit),
};

AtomicExpr: Spanned<Node> = {
    <l:@L> "print" "(" <expr:Expression> ")" <r:@R> =>
        Spanned::new(l, r, Node::new(env.next_id(), NodeKind::Print(Box::new(expr)))),
    <l:@L> "ref" "(" <expr:Expression> ")" <r:@R> =>
        Spanned::new(l, r, Node::new(env.next_id(), NodeKind::Ref(Box::new(expr)))),
    <l:@L> <name:"identifier"> "(" <args:Comma<Expression>> ")" <r:@R> =>
        Spanned::new(l, r, Node::new(env.next_id(), NodeKind::Call(name.to_string(), args))),
    <l:@L> "(" <expr:Expression> ")" <r:@R> => Spanned::new(l, r, Node::new(env.next_id(), NodeKind::Parens(Box::new(expr)))),
    <l:@L> "@" <expr:AtomicExpr> <r:@R> => Spanned::new(l, r, Node::new(env.next_id(), NodeKind::Deref(Box::new(expr)))),

    Span<Literal>,
    SpannedBlockExpr,
    SpannedIfExpr,
    Span<WhileExpr>,
};

Factor: Spanned<Node> = {
    <l:@L> <lhs:Factor> "*" <rhs:AtomicExpr> <r:@R> =>
        Spanned::new(l, r, Node::new(env.next_id(), NodeKind::Infix(Box::new(lhs), Operator::Mul, Box::new(rhs)))),
    <l:@L> <lhs:Factor> "/" <rhs:AtomicExpr> <r:@R> =>
        Spanned::new(l, r, Node::new(env.next_id(), NodeKind::Infix(Box::new(lhs), Operator::Div, Box::new(rhs)))),
    AtomicExpr,
};

AdditiveOp: Operator = {
    "+" => Operator::Add,
    "-" => Operator::Sub,
};

AdditiveExpr: Spanned<Node> = {
    <l:@L> <lhs:AdditiveExpr> <op:AdditiveOp> <rhs:Factor> <r:@R> =>
        Spanned::new(l, r, Node::new(env.next_id(), NodeKind::Infix(Box::new(lhs), op, Box::new(rhs)))),
    Factor,
};

CompareOp: Operator = {
    "==" => Operator::Eq,
    "!=" => Operator::Neq,
    "<=" => Operator::LE,
    "<" => Operator::LT,
    ">=" => Operator::GE,
    ">" => Operator::GT,
};

ComparativeExpr: Spanned<Node> = {
    <l:@L> <lhs:AdditiveExpr> <op:CompareOp> <rhs:AdditiveExpr> <r:@R> =>
        Spanned::new(l, r, Node::new(env.next_id(), NodeKind::Infix(Box::new(lhs), op, Box::new(rhs)))),
    AdditiveExpr,
};

Expression: Spanned<Node> = {
    ComparativeExpr,
};

// statement

LetStmt: Node = {
    "let" <name:"identifier"> <typ:Span<TypeSpec>?> "=" <value:Expression> => {
        let let_ = Let {
            name: name.to_string(),
            typ: typ,
            value: value,
        };
        Node::new(env.next_id(), NodeKind::Let(Box::new(let_)))
    },
};

Assignment: Node = {
    <reference:AtomicExpr> "<-" <value:Expression> => {
        Node::new(env.next_id(), NodeKind::Assign(Box::new(reference), Box::new(value)))
    }
};

Statement: Spanned<Node> = {
    Span<LetStmt>,
    Span<Assignment>,
};

// definition

Def: Def = {
    "def" <name:"identifier"> "(" <args:Comma<Param>> ")" <ret:TypeSpec?> "=" <body:Expression> => {
        Def {
            name: name.to_string(),
            args: args,
            ret: ret,
            body: body,
        }
    },
};

// nodes

AnyNode: Spanned<Node> = {
    Statement,
    Expression,
};

Nodes: Vec<Spanned<Node>> = SepSeq<AnyNode>;

// toplevels

ToplevelLet: ToplevelKind = {
    "let" <name:"identifier"> <typ:Span<TypeSpec>> "=" <value:Expression> => {
        let let_ = Let {
            name: name.to_string(),
            typ: Some(typ),
            value: value,
        };
        ToplevelKind::Let(Box::new(let_))
    },
};

ToplevelKind: ToplevelKind = {
    Def => ToplevelKind::Def(Box::new(<>)),
    ToplevelLet,
    Expression => ToplevelKind::Expr(Box::new(<>)),
};

Toplevel: Toplevel =
    ToplevelKind => Toplevel::new(env.next_id(),  <>)
    ;

pub Program = SepSeq<Span<Toplevel>>;
